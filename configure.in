dnl configure.in for nail, a mail user agent.
dnl Process this file with autoconf to produce a configure script.

# Sccsid: @(#)configure.in	1.12 (gritter) 2/19/02

dnl Initially written by Jacob Moorman.
dnl Changes by Gunnar Ritter.

AC_INIT(mime.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(nail, 0.00)
AC_DEFINE(REL_DATE, "invalid")
AC_ARG_ENABLE(debug, [  --enable-debug          enable debugging build (default=no)],enable_debug=yes,enable_debug=no)
AC_ARG_ENABLE(efence, [  --enable-efence         enable debugging build (with efence) (default=no)],enable_debug=yes enable_efence=yes,enable_efence=no)
AC_ARG_ENABLE(all-chars, [  --enable-all-chars      display all characters ignoring locale (default=no)],enable_all_chars=yes,enable_all_chars=no)

AC_ARG_WITH(mailspool, [  --with-mailspool=DIR    mail spool directory],
	arg_cv_mailspool=$withval,
	AC_CACHE_CHECK(mail spool directory, arg_cv_mailspool,
		for dir in /var/mail /var/spool/mail /usr/mail /usr/spool/mail
		do	if test -d $dir
			then	arg_cv_mailspool=$dir
				break
			fi
		done
		if test "x$arg_cv_mailspool" = x
		then	echo "Use --with-mailspool=dir" >&2
			exit 1
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_MAILDIR, "$arg_cv_mailspool")

AC_ARG_WITH(csh, [  --with-csh=FILE         csh binary],
	arg_cv_csh=$withval,
	AC_CACHE_CHECK(csh binary, arg_cv_csh,
		for dir in /usr/local/bin /bin /usr/bin
		do	if test -x $dir/csh
			then	arg_cv_csh=$dir/csh
				break
			fi
		done
		if test "x$arg_cv_csh" = x
		then	arg_cv_csh=/bin/csh
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_CSHELL, "$arg_cv_csh")

AC_ARG_WITH(more, [  --with-more=FILE        more binary],
	arg_cv_more=$withval,
	AC_CACHE_CHECK(more binary, arg_cv_more,
		for dir in /usr/local/bin /bin /usr/bin
		do	if test -x $dir/more
			then	arg_cv_more=$dir/more
				break
			fi
		done
		if test "x$arg_cv_more" = x
		then	arg_cv_more=/bin/more
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_MORE, "$arg_cv_more")

AC_ARG_WITH(ex, [  --with-ex=FILE          ex binary],
	arg_cv_ex=$withval,
	AC_CACHE_CHECK(ex binary, arg_cv_ex,
		for dir in /usr/local/bin /bin /usr/bin
		do	if test -x $dir/ex
			then	arg_cv_ex=$dir/ex
				break
			fi
		done
		if test "x$arg_cv_ex" = x
		then	arg_cv_ex=/usr/bin/ex
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_EX, "$arg_cv_ex")

AC_ARG_WITH(vi, [  --with-vi=FILE          vi binary],
	arg_cv_vi=$withval,
	AC_CACHE_CHECK(vi binary, arg_cv_vi,
		for dir in /usr/local/bin /bin /usr/bin
		do	if test -x $dir/vi
			then	arg_cv_vi=$dir/vi
				break
			fi
		done
		if test "x$arg_cv_vi" = x
		then	arg_cv_vi=/usr/bin/vi
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_VI, "$arg_cv_vi")

AC_ARG_WITH(sendmail, [  --with-sendmail=FILE    sendmail binary],
	arg_cv_sendmail=$withval,
	AC_CACHE_CHECK(sendmail binary, arg_cv_sendmail,
		for dir in /usr/local/sbin /sbin /usr/sbin /usr/lib
		do	if test -x $dir/sendmail
			then	arg_cv_sendmail=$dir/sendmail
				break
			fi
		done
		if test "x$arg_cv_sendmail" = x
		then	arg_cv_sendmail=/usr/lib/sendmail
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_SENDMAIL, "$arg_cv_sendmail")

AC_ARG_WITH(rcfile,
	[  --with-rcfile=FILE      system-wide configuration (/etc/nail.rc)],
	arg_cv_rcfile=$withval, arg_cv_rcfile="/etc/nail.rc")
AC_DEFINE_UNQUOTED(PATH_MASTER_RC, "$arg_cv_rcfile")

dnl Checks for programs.

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S

dnl Only use -Wall if we have gcc
if test "x$enable_debug" = xyes
then	if test "x$GCC" = xyes
	then	CFLAGS="-Wall -Wno-unused -g -O"
	else	CFLAGS="-g"
	fi
fi

AC_DEFINE_UNQUOTED(PATH_TMP, "/tmp")

dnl Formulate proper paths based on prefix
test "$prefix" = NONE && prefix=/usr/local
dnl We need these seperate to use AC_DEFINED_UNQUOTED
test "$libdir" = '${exec_prefix}/lib' && libdir=${prefix}/lib
test "$sysconfdir" = '/etc' && sysconfdir=/etc

dnl Checks for libraries.
if test "x$enable_efence" = xyes
then	AC_CHECK_LIB(efence, malloc)
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(sys/file.h sys/ioctl.h sys/param.h sys/select.h \
	sys/stat.h sys/utsname.h sys/socket.h netinet/in.h arpa/inet.h)
AC_CHECK_HEADERS(fcntl.h netdb.h signal.h strings.h unistd.h langinfo.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

dnl Checks for constants
AC_DEFINE_UNQUOTED(NSIG, 32)
AC_DEFINE_UNQUOTED(MAXHOSTNAMELEN, 64)
AC_DEFINE_UNQUOTED(NOFILE, 256)

dnl Checks for variables
AC_MSG_CHECKING(for optind in unistd.h)
AC_TRY_COMPILE([#include <unistd.h>], [optind = 0;],
	[AC_MSG_RESULT(yes)], [AC_MSG_RESULT(no); AC_DEFINE(NEED_OPTIND)])

AC_MSG_CHECKING(for optarg in unistd.h)
AC_TRY_COMPILE([#include <unistd.h>], [optarg = (char *)0;],
	[AC_MSG_RESULT(yes)], [AC_MSG_RESULT(no); AC_DEFINE(NEED_OPTARG)])

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(mktime strtol tempnam getopt, ,
	echo "A required function was not found" >&2
	exit 1)
AC_CHECK_FUNCS(gethostname mkstemp select strcasecmp snprintf uname)
AC_CHECK_FUNCS(nl_langinfo)
AC_FUNC_SELECT_ARGTYPES
AC_CHECK_FUNC(gethostent, AC_DEFINE(HAVE_SOCKETS),
	AC_CHECK_LIB(nsl, gethostent, AC_DEFINE(HAVE_SOCKETS)
					AC_DEFINE(HAVE_LIBNSL)
					LIBS="-lnsl $LIBS"
	)
	AC_CHECK_LIB(socket, setsockopt, AC_DEFINE(HAVE_SOCKETS)
					AC_DEFINE(HAVE_LIBSOCKET)
					LIBS="-lsocket $LIBS"
	)
)
if test "x$enable_all_chars" != xyes
then	AC_CHECK_HEADERS(iconv.h, AC_CHECK_FUNCS(iconv, ,
		echo "*** Character set conversion not available ***" >&2),
		echo "*** Character set conversion not available ***" >&2)
	AC_CHECK_HEADERS(locale.h, AC_CHECK_FUNCS(setlocale, ,
		echo "*** Locale support not available ***" >&2),
		echo "*** Locale support not available ***" >&2)
	AC_CHECK_HEADERS(wchar.h wctype.h)
	AC_CHECK_FUNCS(mbtowc iswprint)
fi
AC_OUTPUT(Makefile)
