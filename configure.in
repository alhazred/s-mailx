dnl configure.in for nail, a mail user agent.
dnl Process this file with autoconf to produce a configure script.

# Sccsid: @(#)configure.in	1.6 (gritter) 1/11/01

dnl Initially written by Jacob Moorman.
dnl Changes by Gunnar Ritter.

AC_INIT(mime.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(nail, 0.00)
AC_DEFINE(REL_DATE, "invalid")
AC_ARG_ENABLE(debug, [  --enable-debug          enable debugging build (default=no)],enable_debug=yes,enable_debug=no)
AC_ARG_ENABLE(efence, [  --enable-efence         enable debugging build (with efence) (default=no)],enable_debug=yes enable_efence=yes,enable_efence=no)
AC_ARG_ENABLE(all-chars, [  --enable-all-chars      display all characters ignoring locale (default=no)],enable_all_chars=yes,enable_all_chars=no)

AC_ARG_WITH(mailspool, [  --with-mailspool=DIR    mail spool directory],
	arg_cv_mailspool=$withval,
	AC_CACHE_CHECK(mail spool directory, arg_cv_mailspool,
		for dir in /var/mail /var/spool/mail /usr/mail /usr/spool/mail
		do	if test -d $dir
			then	arg_cv_mailspool=$dir
				break
			fi
		done
		if test "x$arg_cv_mailspool" = x
		then	echo "Use --with-mailspool=dir" >&2
			exit 1
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_MAILDIR, "$arg_cv_mailspool")

AC_ARG_WITH(sendmail, [  --with-sendmail=FILE    sendmail binary],
	arg_cv_sendmail=$withval,
	AC_CACHE_CHECK(sendmail binary, arg_cv_sendmail,
		for dir in /usr/local/sbin /sbin /usr/sbin /usr/lib
		do	if test -x $dir/sendmail
			then	arg_cv_sendmail=$dir/sendmail
				break
			fi
		done
		if test "x$arg_cv_sendmail" = x
		then	arg_cv_sendmail=/usr/lib/sendmail
		fi
	)
)
AC_DEFINE_UNQUOTED(PATH_SENDMAIL, "$arg_cv_sendmail")

dnl Checks for programs.

AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S

dnl Only use -Wall if we have gcc
if test "x$enable_debug" = xyes
then	if test "x$GCC" = xyes
	then	CFLAGS="-Wall -Wno-unused -g -O"
	else	CFLAGS="-g"
	fi
fi

AC_PATH_PROG(PATH_CSHELL, csh, /bin/csh)
AC_DEFINE_UNQUOTED(PATH_CSHELL, "$PATH_CSHELL")
AC_PATH_PROG(PATH_MORE, more, /bin/more)
AC_DEFINE_UNQUOTED(PATH_MORE, "$PATH_MORE")
AC_PATH_PROG(PATH_EX, ex, /usr/bin/ex)
AC_DEFINE_UNQUOTED(PATH_EX, "$PATH_EX")
AC_PATH_PROG(PATH_VI, vi, /usr/bin/vi)
AC_DEFINE_UNQUOTED(PATH_VI, "$PATH_VI")
AC_DEFINE_UNQUOTED(PATH_TMP, "/tmp")

dnl Formulate proper paths based on prefix
test "$prefix" = NONE && prefix=/usr/local
dnl We need these seperate to use AC_DEFINED_UNQUOTED
test "$libdir" = '${exec_prefix}/lib' && libdir=${prefix}/lib
test "$sysconfdir" = '${prefix}/etc' && sysconfdir=${prefix}/etc
AC_DEFINE_UNQUOTED(PATH_HELP, "$libdir/nail.help")
AC_DEFINE_UNQUOTED(PATH_TILDE, "$libdir/nail.tildehelp")
AC_DEFINE_UNQUOTED(PATH_MASTER_RC, "$sysconfdir/nail.rc")

dnl Checks for libraries.
if test "x$enable_efence" = xyes
then	AC_CHECK_LIB(efence, malloc)
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(sys/file.h sys/ioctl.h sys/param.h sys/select.h \
	sys/stat.h sys/utsname.h sys/socket.h netinet/in.h arpa/inet.h)
AC_CHECK_HEADERS(fcntl.h netdb.h signal.h unistd.h langinfo.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

dnl Checks for constants
AC_DEFINE_UNQUOTED(NSIG, 32)
AC_DEFINE_UNQUOTED(MAXHOSTNAMELEN, 64)
AC_DEFINE_UNQUOTED(NOFILE, 256)

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(mktime strtol tempnam getopt, ,
	echo "A required function was not found" >&2
	exit 1)
AC_CHECK_FUNCS(gethostname mkstemp select strcasecmp snprintf uname)
AC_CHECK_FUNCS(nl_langinfo)
AC_FUNC_SELECT_ARGTYPES
AC_CHECK_FUNC(gethostent, AC_DEFINE(HAVE_SOCKETS),
	AC_CHECK_LIB(nsl, gethostent, AC_DEFINE(HAVE_SOCKETS)
					AC_DEFINE(HAVE_LIBNSL)
					LIBS="-lnsl $LIBS"
	)
	AC_CHECK_LIB(socket, setsockopt, AC_DEFINE(HAVE_SOCKETS)
					AC_DEFINE(HAVE_LIBSOCKET)
					LIBS="-lsocket $LIBS"
	)
)
if test "x$enable_all_chars" != xyes
then	AC_CHECK_HEADERS(iconv.h, AC_CHECK_FUNCS(iconv, ,
		echo "*** Character set conversion not available ***" >&2),
		echo "*** Character set conversion not available ***" >&2)
	AC_CHECK_HEADERS(locale.h, AC_CHECK_FUNCS(setlocale, ,
		echo "*** Locale support not available ***" >&2),
		echo "*** Locale support not available ***" >&2)
	AC_CHECK_HEADERS(wchar.h wctype.h)
	AC_CHECK_FUNCS(mbtowc)
fi
AC_OUTPUT(Makefile misc/Makefile)
